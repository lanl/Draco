#--------------------------------------------*-cmake-*---------------------------------------------#
# file   src/compton/CMakeLists.txt
# author Kendra Keady
# date   2017 February 28
# brief  Instructions for building Compton Makefiles.
# note   Copyright (C) 2017-2020 Triad National Security, LLC.
#        All rights reserved.
#--------------------------------------------------------------------------------------------------#
cmake_minimum_required(VERSION 3.9.0)
project( compton CXX )

# This packages requires libcompton.  See draco/config/vendor_libraries.cmake
# and draco/config/FindCOMPTON.cmake to manage how the build system discovers
# this TPL.  If not found, only a stub header file will be installed.

#--------------------------------------------------------------------------------------------------#
# Source files
#--------------------------------------------------------------------------------------------------#
configure_file( config.h.in ${PROJECT_BINARY_DIR}/compton/config.h )

set( sources "${PROJECT_SOURCE_DIR}/Compton.cc" )
set( headers
  ${PROJECT_BINARY_DIR}/compton/config.h
  ${PROJECT_SOURCE_DIR}/Compton.hh )

if( TARGET COMPTON::compton )

  # -------------------------------------------------------------------------- #
  # Build package library
  # -------------------------------------------------------------------------- #

  add_component_library(
    TARGET       Lib_compton
    TARGET_DEPS  "Lib_c4;COMPTON::compton"
    INCLUDE_DIRS "PUBLIC;$<BUILD_INTERFACE:${PROJECT_BINARY_DIR}>"
    LIBRARY_NAME compton
    SOURCES      "${sources}"
    HEADERS      "${headers}" )

  # Copy necessary dll files to the build directory
  if( MSVC AND DRACO_LIBRARY_TYPE STREQUAL SHARED )
    add_custom_command( TARGET Lib_compton POST_BUILD
      COMMAND ${CMAKE_COMMAND} -E copy_if_different
              $<TARGET_FILE:COMPTON::compton> $<TARGET_FILE_DIR:Lib_compton> )
  endif()

  # -------------------------------------------------------------------------- #
  # Installation instructions
  # -------------------------------------------------------------------------- #
  install( FILES ${headers} DESTINATION ${DBSCFGDIR}include/compton )

  # -------------------------------------------------------------------------- #
  # Unit tests
  # -------------------------------------------------------------------------- #
  if( BUILD_TESTING )
    add_subdirectory( test )
  endif()

  # -------------------------------------------------------------------------- #
  # Autodoc
  # -------------------------------------------------------------------------- #
  process_autodoc_pages()

else()

  # If no Compton library, only install headers (stubs).
  install( FILES ${headers} DESTINATION ${DBSCFGDIR}include/compton )

endif()

#--------------------------------------------------------------------------------------------------#
# End compton/CMakeLists.txt
#--------------------------------------------------------------------------------------------------#
